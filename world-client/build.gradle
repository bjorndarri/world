plugins {
    id "java-library"
    id "world.jasperreports.modules"
    id "world.spotless.plugin"
    id "io.github.f-cramer.jasperreports" version "0.0.1"
}

dependencies {
    implementation project(":world-domain-api")

    implementation(libs.codion.framework.json.domain)
    implementation(libs.codion.swing.framework.ui)
    implementation(libs.codion.plugin.jasperreports)

    implementation(libs.json)
    implementation(libs.jfreechart)
    implementation(libs.flatlaf)
    implementation(libs.flatlaf.intellij.themes)
    implementation(libs.ikonli.foundation)

    implementation(libs.jxmapviewer2) {
        transitive = false
    }

    compileOnly(libs.jasperreports.jdt) {
        exclude group: "net.sf.jasperreports"
    }
    implementation(libs.jasperreports.pdf) {
        exclude group: "net.sf.jasperreports"
    }
    implementation(libs.jasperreports.fonts)

    runtimeOnly(libs.codion.plugin.logback.proxy)

    testImplementation project(":world-domain")
    testImplementation(libs.codion.framework.db.local)
    testRuntimeOnly(libs.codion.dbms.h2)
    testRuntimeOnly(libs.h2)
    testImplementation(libs.junit.api)
    testRuntimeOnly(libs.junit.engine)
}

extraJavaModuleInfo {
    automaticModule("net.sf.jasperreports:jasperreports-jdt", "jasperreports.jdt")
    automaticModule("org.eclipse.jdt:ecj", "org.eclipse.jdt.ecj")
}

jasperreports {
    classpath.from(
        project.sourceSets.main.java.classesDirectory,
        configurations.named(project.sourceSets.main.compileClasspathConfigurationName)
    )
}

sourceSets.main.resources.srcDir(compileAllReports)

test {
    useJUnitPlatform()
    systemProperty "codion.test.user", "scott:tiger"
    systemProperty "codion.db.url", "jdbc:h2:mem:h2db"
    systemProperty "codion.db.initScripts", "classpath:create_schema.sql"
}

tasks.register("writeVersion") {
    group "build"
    def versionFile = file(sourceSets.main.output.resourcesDir.getAbsolutePath() + "/version.properties")
    inputs.property("version", project.version)
    outputs.file(versionFile)
    outputs.upToDateWhen { false }

    doLast {
        def versionProperties = new Properties()
        versionProperties.put("version", project.version)
        versionProperties.store(versionFile.newWriter(), null)
    }
}

classes.dependsOn writeVersion