plugins {
    id "org.beryx.jlink"
    id "world.jasperreports"
    id "world.spotless"
}

dependencies {
    implementation project(":world-client")

    runtimeOnly(libs.codion.framework.db.http)
}

applicationDefaultJvmArgs = [
        "-Xmx128m",
        "-Dcodion.client.connectionType=http",
        "-Dcodion.client.http.secure=false",
        "-Dcodion.client.http.hostname=${serverHost}",
        "-Dcodion.client.http.port=${serverHttpPort}",
        "-Dsun.awt.disablegrab=true"
]

application {
    mainModule.set("is.codion.framework.demos.world.client")
    mainClass.set("is.codion.framework.demos.world.ui.WorldAppPanel")
}

jlink {
    imageName.set(project.name)
    moduleName.set(application.mainModule)
    options = ["--strip-debug", "--no-header-files", "--no-man-pages", "--add-modules",
               "jdk.crypto.ec,is.codion.framework.db.http,is.codion.plugin.logback.proxy"]

    addExtraDependencies("jackson-core", "jackson-databind", "jackson-annotation")

    launcher {
        jvmArgs = applicationDefaultJvmArgs
    }

    jpackage {
        imageName = "World-Http"
        if (org.gradle.internal.os.OperatingSystem.current().linux) {
            installerType = "deb"
            icon = "../world.png"
            installerOptions = [
                    "--resource-dir", "build/jpackage/World-Http/lib",
                    "--linux-shortcut"
            ]
        }
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            installerType = "msi"
            icon = "../world.ico"
            installerOptions = [
                    "--win-menu",
                    "--win-shortcut"
            ]
        }
    }
}

prepareMergedJarsDir.doLast {
    copy {
        from "src/main/resources"
        into "$jlinkBasePath/mergedjars"
    }
}