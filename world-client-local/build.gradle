plugins {
    id "org.beryx.jlink"
    id "world.jasperreports.pdf"
    id "world.spotless"
}

dependencies {
    implementation project(":world-client")

    runtimeOnly project(":world-domain")

    runtimeOnly(libs.codion.framework.db.local)
    runtimeOnly(libs.codion.dbms.h2)
    runtimeOnly(libs.h2)
}

applicationDefaultJvmArgs = [
        "-Xmx128m",
        "-Dcodion.client.connectionType=local",
        "-Dcodion.db.url=jdbc:h2:mem:h2db",
        "-Dcodion.db.initScripts=classpath:create_schema.sql",
        "-Dsun.awt.disablegrab=true"
]

application {
    mainModule.set("is.codion.framework.demos.world.client")
    mainClass.set("is.codion.framework.demos.world.ui.WorldAppPanel")
}

jlink {
    imageName.set(project.name)
    moduleName.set(application.mainModule)
    options = ["--strip-debug", "--no-header-files", "--no-man-pages", "--add-modules",
               "jdk.crypto.ec,is.codion.framework.db.local,is.codion.dbms.h2," +
                       "is.codion.plugin.logback.proxy,is.codion.framework.demos.world.domain"]

    addExtraDependencies("slf4j-api")

    prepareMergedJarsDir {
        doLast {
            //https://github.com/TIBCOSoftware/jasperreports/issues/463
            project.delete(files("${mergedJarsDir}/net/sf/jasperreports/fonts"))
        }
    }

    launcher {
        jvmArgs = applicationDefaultJvmArgs
    }

    jpackage {
        imageName = "World-Local"
        if (org.gradle.internal.os.OperatingSystem.current().linux) {
            installerType = "deb"
            icon = "../world.png"
            installerOptions = [
                    "--resource-dir", "build/jpackage/World-Local/lib",
                    "--linux-shortcut"
            ]
        }
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            installerType = "msi"
            icon = "../world.ico"
            installerOptions = [
                    "--win-menu",
                    "--win-shortcut"
            ]
        }
    }
}